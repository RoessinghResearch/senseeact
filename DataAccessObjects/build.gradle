plugins {
	id 'java'
	id 'java-library'
}

group = 'nl.rrd'
version = 5.0

base {
	archivesName = 'senseeact-dao'
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

dependencies {
	api project (':RRDUtils')
	api 'org.mariadb.jdbc:mariadb-java-client:3.5.0'
	testImplementation 'junit:junit:4.13.2'
	testImplementation 'org.slf4j:slf4j-simple:2.0.16'
}

tasks.withType(JavaCompile) {
	options.deprecation = true
}

test {
	testLogging.showStandardStreams = true
}

gradle.taskGraph.whenReady {
	tasks.withType(Test) {
		project.properties.each { key, value ->
			if (!key.matches('test.+') || !(value instanceof String))
				return
			systemProperty key, value
		}
	}
}

compileJava {
	options.compilerArgs = ["-Xlint:deprecation"]
}

javadoc {
	options {
		links 'https://docs.oracle.com/en/java/javase/17/docs/api/'
		addBooleanOption('Xdoclint:all,-missing,-html', true)
	}
}
