plugins {
	id 'java'
	id 'java-library'
}

group = 'nl.rrd'
version = '5.0'

base {
	archivesName = 'senseeact-service-lib'
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

dependencies {
	api project(':SenSeeActClient')
	api 'com.nimbusds:nimbus-jose-jwt:9.37.3'
	api 'jakarta.mail:jakarta.mail-api:2.1.2'
	api 'org.eclipse.angus:angus-mail:2.0.2'
	api 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'
	api 'org.springframework:spring-context-support:6.1.3'
	api('org.springframework.boot:spring-boot-starter-web:3.2.2') {
		exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
		// logback-classic should only be enabled at runtime. It should not be
		// used during test.
		exclude group: 'ch.qos.logback', module: 'logback-classic'
	}
	api 'com.google.cloud:libraries-bom:26.31.0'
	api 'com.google.cloud:google-cloud-texttospeech:2.35.0'
	api('com.google.firebase:firebase-admin:9.2.0') {
		// firebase-admin includes a version of slf4j-api that is not compatible
		// with the latest version of spring-boot
		exclude group: 'org.slf4j', module: 'slf4j-api'
	}
}

tasks.withType(JavaCompile) {
	options.deprecation = true
}

javadoc {
	options {
		links 'https://docs.oracle.com/en/java/javase/17/docs/api/'
		addBooleanOption('Xdoclint:all,-missing,-html', true)
	}
}
